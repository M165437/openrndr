plugins {
    alias(libs.plugins.nebula.release)
    alias(libs.plugins.gradle.nexus.publish)
    id("org.openrndr.convention.dokka")
}

repositories {
    mavenCentral()
}

group = "org.openrndr"
nexusPublishing {
    repositories {
        sonatype {
            username.set(findProperty("ossrhUsername") ?: System.getenv("OSSRH_USERNAME"))
            password.set(findProperty("ossrhPassword") ?: System.getenv("OSSRH_PASSWORD"))
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots"))
        }
    }
}

subprojects {
    // Equivalent Kotlin is: tasks.register<DependencyReportTask>("dependenciesAll") { ...
    tasks.register("dependenciesAll", DependencyReportTask) {
        group = HelpTasksPlugin.HELP_GROUP
        description = "Displays all dependencies, including subprojects."
    }
}

dokkaHtmlMultiModule {
    dependsOn("openrndr-js:dokkaHtmlMultiModule")
    dependsOn("openrndr-jvm:dokkaHtmlMultiModule")
}

gradle.buildFinished {
    println("\n")
    println("openrndr = \"${version}\"")
}
